@echo off
setlocal EnableDelayedExpansion
chcp 850 > nul

echo ====== Verificando disponibilidad de Git =======================
where git >nul 2>null
if %ERRORLEVEL% EQU 0 (
    echo +++ Git esta preinstalado
) else (
    set "PATH=%~dp0..\mingit\cmd;%PATH%"
)

echo ==== Quitando previos Commander y GUI ==================
taskkill /IM "ecuapass_commander.exe" /F 2>nul 
taskkill /FI "WINDOWTITLE eq EcuapassBot" /F


REM echo ==== Obteniendo Ultimo release en GitHub ==================
REM for /f %%A in ('
REM   powershell -NoProfile -Command ^
REM     "(Invoke-RestMethod -Headers @{\"User-Agent\"=\"batch\"} -Uri https://api.github.com/repos/lgsof/EcuapassBot7-win/releases/latest).tag_name"
REM ') do set "LATEST_TAG=%%A"
REM 
REM if not defined LATEST_TAG (
REM   echo ERROR: No se pudo obtener el último tag desde GitHub.
REM   goto :ejecutar_app
REM )

echo ==== Obteniendo Ultimo release en GitHub ==================
set "LATEST_TAG="
for /f %%A in ('
  powershell -NoProfile -Command ^
    "$tag = try { (Invoke-RestMethod -Headers @{\"User-Agent\"=\"EcuapassBot\"} -Uri https://api.github.com/repos/lgsof/EcuapassBot7-wintest/releases/latest -TimeoutSec 10).tag_name } catch { $null }; ^
     if ($tag -and $tag -notmatch \"error|exception|fail\" -and $tag -match \"^v?\d\") { $tag } else { \"INVALID\" }"
') do (
  if not "%%A"=="INVALID" set "LATEST_TAG=%%A"
)

if not defined LATEST_TAG (
  echo ERROR: No se pudo obtener el último tag desde GitHub o conexion fallida.
  goto :ejecutar_app
)

echo Ultimo tag: !LATEST_TAG!

echo ==== Leyendo VERSION.txt ========================
if not exist VERSION.txt (
    echo ERROR: Archivo VERSION.txt no encontrado.
    goto :ejecutar_actualizacion
)
set /p "LOCAL_TAG=" < VERSION.txt

echo --------------------------------------------------------
echo Version remota : !LATEST_TAG!
echo Version local  : !LOCAL_TAG!
echo --------------------------------------------------------

:: === Comparar versiones
if "!LATEST_TAG!"=="!LOCAL_TAG!" (
    echo +++ Version local ya estaba actualizada.
    goto :ejecutar_app
)

:ejecutar_actualizacion

echo ====== Verificando si existe repositorio Git ===================
if not exist ".git" (
    echo ERROR: Carpeta .git no encontrada. Se omite la actualizacion.
    goto :ejecutar_app
)

echo ====== Evitando que se actualize el commander =======================
git update-index --skip-worktree ecuapass_commander\ecuapass_commander.exe

echo ====== Buscando actualizaciones ================================
git fetch origin main
if %ERRORLEVEL% EQU 1 (
    echo ADVERTENCIA: Fallo en git fetch. Se omite la actualizacion.
    goto :ejecutar_app
)

echo ====== Archivos que se actualizan ==============================
git --no-pager diff --name-status HEAD origin/main

echo ====== Aplicando actualizaciones ===============================
git reset --hard origin/main
if %ERRORLEVEL% EQU 1 (
    echo ADVERTENCIA: Fallo en git reset. Continuando con los archivos actuales.
)

echo ====== Parchando Commander =====================================
call patches\ebotpatch-update-exe-win.bat

echo ====== Actualizando VERSION.txt ================================
echo !LATEST_TAG!>VERSION.txt

:ejecutar_app
echo ======= Ejecutando EcuapassBot =================================
set JAVA=javaw
if "%~1"=="debug" (
    set JAVA=java
)

start bin\jre-1.8\bin\%JAVA% -jar "bin\EcuapassBotGUI.jar"

